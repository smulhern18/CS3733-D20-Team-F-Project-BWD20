plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.27.1'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "io.freefair.lombok" version "5.0.0-rc6"
}



mainClassName = 'edu.wpi.teamF.Main'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://apisite.crmyers.dev/maven'
    }
}

dependencies {
    implementation(
            'com.jfoenix:jfoenix:9.0.9',

            // You may comment out the database dependency you do not use
            'org.xerial:sqlite-jdbc:3.30.1',
            'org.apache.derby:derby:10.14.2.0',

            'org.slf4j:slf4j-api:1.7.30',
            'org.slf4j:slf4j-simple:1.7.30',
    )

    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.6.0',
            'org.testfx:testfx-junit5:4.0.16-alpha',
    )
    testRuntimeOnly(
            'org.testfx:openjfx-monocle:8u76-b04',
    )
    implementation 'org.testng:testng:6.9.6'
}

javafx {
    version = "11"
    modules = ['javafx.controls', 'javafx.fxml']
}


test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

run {
    jvmArgs = ['--add-opens=javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
               '--add-opens=javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',
               '--add-opens=javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED',
               '--add-opens=javafx.base/com.sun.javafx.binding=ALL-UNNAMED',
               '--add-opens=javafx.base/com.sun.javafx.event=ALL-UNNAMED',
               '--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED',
               '--add-opens=javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED'
    ]
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
            excludes = [
                    'edu.wpi.teamF.App',
                    'edu.wpi.teamF.Main',
            ]
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.25
            }
            excludes = [
                    'edu.wpi.teamF.App',
                    'edu.wpi.teamF.Main',
            ]
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.25
            }
            excludes = [
                    'edu.wpi.teamF.App',
                    'edu.wpi.teamF.Main',
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport

compileJava.dependsOn 'spotlessApply'

// uncomment to enable spotless checking
spotless {
    java {
        googleJavaFormat()
    }
}
